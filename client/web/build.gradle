buildscript {
   repositories {
      jcenter()
      mavenCentral()
   }

   dependencies {
      classpath "com.moowork.gradle:gradle-node-plugin:1.3.1"
   }
}

plugins {
   id 'com.moowork.node' version '1.3.1'
}

node {
   version = '10.17.0'
   npmVersion = '6.11.3'
   download = true
   workDir = file("${project.gradle.gradleUserHomeDir}/caches/tools")
}

task generateIndexFiles(type: NpmTask, dependsOn: npmInstall) {
   group 'build'
   description 'Generate index files.'
   args = ['run', 'generate-index-files']
}

task compile(type: NpmTask, dependsOn: npmInstall) {
   group 'build'
   description 'Compile angular'
   args = ['run', 'build']
}

task lint(type: NpmTask, dependsOn: npmInstall) {
   group 'verification'
   description = 'Runs the linter on the TypeScript source code'
   args = ['run', 'lint']
}

task test(type: NpmTask, dependsOn: lint) {
   group 'verification'
   description = "Runs the tests, including linting"
   args = ['run', 'test']
}

task build(dependsOn: [compile, test]) {
   group 'build'
   description = 'Build web'
}

task webCodeTestReport(type: NpmTask) {
   group 'verification'
   description = "Runs the report-coverage after test."
   args = ['run', 'report-coverage']
}

task cleanChunk(type: Delete) {
   group 'build'
   delete = ['../server/build/resources/main/static/app', 'dist', 'build']
}

task clean(dependsOn: [cleanChunk]) {
   group 'build'
}

task cleanAll(type: Delete, dependsOn: [clean]) {
   group 'build'
   delete = ['node_modules', '.gradle']
}

task watch(type: NpmTask, dependsOn: npmInstall) {
   group 'build'
   description 'watch angular compile'

   args = ['run', 'build:watch']
}
